import Mathlib.AlgebraicGeometry.Morphisms.FinitePresentation
import Mathlib.AlgebraicGeometry.Morphisms.Flat
import Mathlib.AlgebraicGeometry.Morphisms.UniversallyClosed
import Mathlib.AlgebraicGeometry.Morphisms.UniversallyInjective
import Mathlib.RingTheory.Ideal.GoingDown
import Mathlib.RingTheory.Spectrum.Prime.Chevalley
import Pi1.Mathlib.AlgebraicGeometry.Morphisms.RingHomProperties
import Pi1.Mathlib.AlgebraicGeometry.Morphisms.FinitePresentation
import Pi1.Mathlib.CategoryTheory.MorphismProperty.Limits
import Pi1.Mathlib.AlgebraicGeometry.Morphisms.UniversallyOpen
import Pi1.Mathlib.RingTheory.RingHom.Flat
import Pi1.Mathlib.RingTheory.Spectrum.Prime.Topology

universe u v

open TensorProduct

section

@[algebraize Module.FaithfullyFlat]
def RingHom.FaithfullyFlat {R S : Type*} [CommRing R] [CommRing S] (f : R ‚Üí+* S) : Prop :=
  letI : Algebra R S := f.toAlgebra
  Module.FaithfullyFlat R S

variable {R S : Type*} [CommRing R] [CommRing S] (f : R ‚Üí+* S)

lemma RingHom.faithfullyFlat_algebraMap_iff [Algebra R S] :
    (algebraMap R S).FaithfullyFlat ‚Üî Module.FaithfullyFlat R S := by
  simp only [RingHom.FaithfullyFlat]
  congr!
  exact Algebra.algebra_ext _ _ fun _ ‚Ü¶ rfl

lemma RingHom.FaithfullyFlat.flat (hf : f.FaithfullyFlat) : f.Flat := by
  algebraize [f]
  exact inferInstanceAs <| Module.Flat R S

lemma RingHom.FaithfullyFlat.iff_flat_and_comap_surjective :
    f.FaithfullyFlat ‚Üî f.Flat ‚àß Function.Surjective f.specComap := by
  algebraize [f]
  show (algebraMap R S).FaithfullyFlat ‚Üî (algebraMap R S).Flat ‚àß
    Function.Surjective (algebraMap R S).specComap
  rw [RingHom.faithfullyFlat_algebraMap_iff, RingHom.flat_algebraMap_iff]
  exact ‚ü®fun h ‚Ü¶ ‚ü®inferInstance, PrimeSpectrum.specComap_surjective_of_faithfullyFlat‚ü©,
    fun ‚ü®h, hf‚ü© ‚Ü¶ .of_specComap_surjective hf‚ü©

lemma Module.FaithfullyFlat.bijective_of_tensorProduct [Algebra R S]
    {T : Type*} [CommRing T] [Algebra R T] [Module.FaithfullyFlat R S]
    (H : Function.Bijective (algebraMap S (S ‚äó[R] T))) :
    Function.Bijective (algebraMap R T) := by
  refine ‚ü®?_, ?_‚ü©
  ¬∑ apply (Module.FaithfullyFlat.lTensor_injective_iff_injective R S (Algebra.linearMap R T)).mp
    have : LinearMap.lTensor S (Algebra.linearMap R T) =
        Algebra.linearMap S (S ‚äó[R] T) ‚àò‚Çó (AlgebraTensorModule.rid R S S).toLinearMap := by
      ext; simp
    simpa [this] using H.1
  ¬∑ apply (Module.FaithfullyFlat.lTensor_surjective_iff_surjective R S (Algebra.linearMap R T)).mp
    have : LinearMap.lTensor S (Algebra.linearMap R T) =
        Algebra.linearMap S (S ‚äó[R] T) ‚àò‚Çó (AlgebraTensorModule.rid R S S).toLinearMap := by
      ext; simp
    simpa [this] using H.2

lemma RingHom.Bijective.stableUnderComposition :
    RingHom.StableUnderComposition (fun f ‚Ü¶ Function.Bijective f) :=
  fun _ _ _ _ _ _ _ _ hf hg ‚Ü¶ hg.comp hf

lemma RingHom.Bijective.respectsIso :
    RingHom.RespectsIso (fun f ‚Ü¶ Function.Bijective f) :=
  RingHom.Bijective.stableUnderComposition.respectsIso fun e ‚Ü¶ e.bijective

lemma RingHom.FaithfullyFlat.bijective_codescendsAlong :
    RingHom.CodescendsAlong (fun f ‚Ü¶ Function.Bijective f) RingHom.FaithfullyFlat := by
  apply RingHom.CodescendsAlong.mk
  ¬∑ exact RingHom.Bijective.respectsIso
  ¬∑ introv h H
    rw [RingHom.faithfullyFlat_algebraMap_iff] at h
    exact h.bijective_of_tensorProduct H

end

open CategoryTheory Limits MorphismProperty

namespace AlgebraicGeometry

lemma exists_preimage_of_isPullback {P X Y Z : Scheme.{u}} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y}
    {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} (h : IsPullback fst snd f g) (x : X) (y : Y)
    (hxy : f.base x = g.base y) :
    ‚àÉ (p : P), fst.base p = x ‚àß snd.base p = y := by
  let e := h.isoPullback
  obtain ‚ü®z, hzl, hzr‚ü© := AlgebraicGeometry.Scheme.Pullback.exists_preimage_pullback x y hxy
  use h.isoPullback.inv.base z
  simp [‚Üê Scheme.comp_base_apply, hzl, hzr]

lemma image_preimage_eq_of_isPullback {P X Y Z : Scheme.{u}} {fst : P ‚ü∂ X} {snd : P ‚ü∂ Y}
    {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} (h : IsPullback fst snd f g) (s : Set X) :
    snd.base '' (fst.base ‚Åª¬π' s) = g.base ‚Åª¬π' (f.base '' s) := by
  refine subset_antisymm ?_ (fun x hx ‚Ü¶ ?_)
  ¬∑ rw [Set.image_subset_iff, ‚Üê Set.preimage_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, ‚Üê h.1.1]
    rw [Scheme.comp_base, TopCat.coe_comp, ‚Üê Set.image_subset_iff, Set.image_comp]
    exact Set.image_mono (Set.image_preimage_subset _ _)
  ¬∑ obtain ‚ü®y, hy, heq‚ü© := hx
    obtain ‚ü®o, hl, hr‚ü© := exists_preimage_of_isPullback h y x heq
    use o
    simpa [hl, hr]

instance : IsStableUnderComposition @Surjective where
  comp_mem _ _ hf hg := ‚ü®hg.1.comp hf.1‚ü©

instance Flat.surjective_descendsAlong_surjective_inf_flat_inf_quasicompact :
    DescendsAlong @Surjective (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
  have : RespectsRight (@Surjective) (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
    constructor
    introv hi hf
    exact MorphismProperty.comp_mem _ _ _ hf hi.1.1
  have : HasOfPrecompProperty (@Surjective) (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
    constructor
    introv hf hcomp
    exact Surjective.of_comp f g
  infer_instance

instance {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [Flat g] : Flat (pullback.fst f g) :=
  pullback_fst _ _ inferInstance

instance {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [Flat f] : Flat (pullback.snd f g) :=
  pullback_snd _ _ inferInstance

instance {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [Surjective g] :
    Surjective (pullback.fst f g) :=
  pullback_fst _ _ inferInstance

instance {X Y Z : Scheme.{u}} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [Surjective f] :
    Surjective (pullback.snd f g) :=
  pullback_snd _ _ inferInstance

lemma QuasiCompact.compactSpace_of_compactSpace {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [QuasiCompact f]
      [CompactSpace Y] : CompactSpace X := by
  constructor
  rw [‚Üê Set.preimage_univ (f := f.base)]
  exact QuasiCompact.isCompact_preimage _ isOpen_univ CompactSpace.isCompact_univ

@[stacks 02JY]
lemma Flat.isQuotientMap_of_surjective {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [Flat f] [QuasiCompact f]
    [Surjective f] : Topology.IsQuotientMap f.base := by
  rw [Topology.isQuotientMap_iff]
  refine ‚ü®f.surjective, fun s ‚Ü¶ ‚ü®fun hs ‚Ü¶ hs.preimage f.continuous, fun hs ‚Ü¶ ?_‚ü©‚ü©
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ let ùí∞ := Y.affineCover
    rw [ùí∞.isOpenCover_opensRange.isOpen_iff_inter]
    intro i
    rw [Scheme.Hom.coe_opensRange, ‚Üê Set.image_preimage_eq_inter_range]
    apply (ùí∞.map i).isOpenMap
    refine this (f := pullback.fst (ùí∞.map i) f) _ ?_ ‚ü®_, rfl‚ü©
    rw [‚Üê Set.preimage_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.comp_base, pullback.condition,
      Scheme.comp_base, TopCat.coe_comp, Set.preimage_comp]
    exact hs.preimage (Scheme.Hom.continuous _)
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ have _ : CompactSpace X := QuasiCompact.compactSpace_of_compactSpace f
    let ùí∞ := X.affineCover.finiteSubcover
    let T := ‚àê (fun i : ùí∞.J ‚Ü¶ ùí∞.obj i)
    let p : ‚àê (fun i : ùí∞.J ‚Ü¶ ùí∞.obj i) ‚ü∂ X := Sigma.desc (fun i ‚Ü¶ ùí∞.map i)
    have _ (i : ùí∞.J) : IsAffine (ùí∞.obj i) := inferInstanceAs <| IsAffine (X.affineCover.obj _)
    have _ : Flat p := by
      rw [IsLocalAtSource.iff_of_openCover (P := @Flat) (sigmaOpenCover _)]
      exact fun i ‚Ü¶ by simpa [p] using IsLocalAtSource.of_isOpenImmersion _
    have _ : Surjective p := ‚ü®fun x ‚Ü¶ by
      obtain ‚ü®i, x, rfl‚ü© := X.affineCover.finiteSubcover.exists_eq x
      use (Sigma.Œπ (fun i ‚Ü¶ X.affineCover.finiteSubcover.obj i) i).base x
      rw [‚Üê Scheme.comp_base_apply, Sigma.Œπ_desc]‚ü©
    refine this (f := T.isoSpec.inv ‚â´ p ‚â´ f) _ _ ?_ ‚ü®_, rfl‚ü©
    rw [‚Üê Category.assoc, Scheme.comp_base, TopCat.coe_comp, Set.preimage_comp]
    exact hs.preimage (T.isoSpec.inv ‚â´ p).continuous
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  refine ((PrimeSpectrum.isQuotientMap_of_generalizingMap ?_ ?_).isOpen_preimage).mp hs
  exact (surjective_iff (Spec.map œÜ)).mp inferInstance
  apply RingHom.Flat.generalizingMap_comap
  rwa [‚Üê HasRingHomProperty.Spec_iff (P := @Flat)]

lemma _root_.CategoryTheory.Limits.isPullback_map_snd_snd {C : Type*} [Category C] [HasPullbacks C]
    {X Y Z S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) (h : Z ‚ü∂ S) :
    IsPullback (pullback.map _ _ _ _ (pullback.snd f g) (pullback.snd f h) f
        pullback.condition pullback.condition)
      (pullback.fst (pullback.fst f g) (pullback.fst f h))
      (pullback.fst g h) (pullback.snd f g) := by
  refine ‚ü®‚ü®by simp‚ü©, ‚ü®PullbackCone.IsLimit.mk _ ?_ ?_ ?_ ?_‚ü©‚ü©
  ¬∑ intro c
    refine pullback.lift c.snd
        (pullback.lift (c.snd ‚â´ pullback.fst _ _) (c.fst ‚â´ pullback.snd _ _) ?_) ?_
    ¬∑ simp [pullback.condition, ‚Üê c.condition_assoc]
    ¬∑ simp
  ¬∑ intro c
    apply pullback.hom_ext <;> simp [c.condition]
  ¬∑ intro c
    apply pullback.hom_ext <;> simp [c.condition]
  ¬∑ intro c m hfst hsnd
    apply pullback.hom_ext
    ¬∑ simpa
    ¬∑ apply pullback.hom_ext <;> simp [‚Üê hsnd, pullback.condition, ‚Üê hfst]

lemma _root_.CategoryTheory.Limits.isPullback_map_fst_fst {C : Type*} [Category C] [HasPullbacks C]
    {X Y Z U S : C} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) (i : Z ‚ü∂ S) (h : U ‚ü∂ pullback i g) :
    IsPullback
      (pullback.map (pullback.snd f g) (h ‚â´ pullback.snd i g) f i
        (pullback.fst f g) (h ‚â´ pullback.fst i g) g
        pullback.condition.symm (by simp [pullback.condition]))
      (pullback.snd (pullback.snd f g) (h ‚â´ pullback.snd i g))
      (pullback.snd f i)
      (h ‚â´ pullback.fst i g) := by
  refine ‚ü®‚ü®by simp‚ü©, ‚ü®PullbackCone.IsLimit.mk _ ?_ ?_ ?_ ?_‚ü©‚ü©
  ¬∑ intro c
    exact pullback.lift (pullback.lift (c.fst ‚â´ pullback.fst _ _) (c.snd ‚â´ h ‚â´ pullback.snd _ _)
          (by simp [pullback.condition, c.condition_assoc])) c.snd (by simp)
  ¬∑ intro c
    apply pullback.hom_ext <;> simp [c.condition]
  ¬∑ intro c
    simp
  ¬∑ intro c m hfst hsnd
    apply pullback.hom_ext
    ¬∑ apply pullback.hom_ext
      simp [‚Üê hfst]
      simp [‚Üê hsnd, pullback.condition]
    ¬∑ simpa

/-- Universally closed satisfies fpqc descent. -/
@[stacks 02KS]
instance Flat.universallyClosed_descendsAlong_surjective_inf_flat_inf_quasicompact :
    DescendsAlong @UniversallyClosed (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
  refine IsLocalAtTarget.descendsAlong_inf_quasiCompact _ _ ?_ ?_
  ¬∑ exact fun {X} _ ‚Ü¶ X.exists_hom_isAffine_of_isLocalAtSource _ @Flat le_rfl
  refine fun {R} S Y œÜ g ‚ü®_, _‚ü© hfst ‚Ü¶ ‚ü®universally_mk' _ _ fun {T} f _ s hs ‚Ü¶ ?_‚ü©
  let p := pullback.fst (pullback.fst (Spec.map œÜ) f) (pullback.fst (Spec.map œÜ) g)
  let r : pullback (pullback.fst (Spec.map œÜ) f) (pullback.fst (Spec.map œÜ) g) ‚ü∂ pullback f g :=
    pullback.map _ _ _ _ (pullback.snd _ _) (pullback.snd _ _) (Spec.map œÜ) (pullback.condition ..)
      (pullback.condition ..)
  have : IsClosed ((pullback.snd (Spec.map œÜ) f).base ‚Åª¬π' ((pullback.fst f g).base '' s)) := by
    rw [‚Üê image_preimage_eq_of_isPullback (isPullback_map_snd_snd ..)]
    exact p.isClosedMap _ (hs.preimage r.continuous)
  rwa [(Flat.isQuotientMap_of_surjective _).isClosed_preimage] at this

/-- Universally open satisfies fpqc descent. -/
@[stacks 02KT]
instance Flat.universallyOpen_descendsAlong_surjective_inf_flat_inf_quasicompact :
    DescendsAlong @UniversallyOpen
      (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
  refine IsLocalAtTarget.descendsAlong_inf_quasiCompact _ _ ?_ ?_
  ¬∑ exact fun {X} _ ‚Ü¶ X.exists_hom_isAffine_of_isLocalAtSource _ @Flat le_rfl
  refine fun {R} S Y œÜ g ‚ü®_, _‚ü© hfst ‚Ü¶ ‚ü®universally_mk' _ _ fun {T} f _ s hs ‚Ü¶ ?_‚ü©
  let p := pullback.fst (pullback.fst (Spec.map œÜ) f) (pullback.fst (Spec.map œÜ) g)
  let r : pullback (pullback.fst (Spec.map œÜ) f) (pullback.fst (Spec.map œÜ) g) ‚ü∂ pullback f g :=
    pullback.map _ _ _ _ (pullback.snd _ _) (pullback.snd _ _) (Spec.map œÜ) (pullback.condition ..)
      (pullback.condition ..)
  have : IsOpen ((pullback.snd (Spec.map œÜ) f).base ‚Åª¬π' ((pullback.fst f g).base '' s)) := by
    rw [‚Üê image_preimage_eq_of_isPullback (isPullback_map_snd_snd ..)]
    exact p.isOpenMap _ (hs.preimage r.continuous)
  rwa [(Flat.isQuotientMap_of_surjective _).isOpen_preimage] at this

lemma flat_and_surjective_SpecMap_iff {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    Flat (Spec.map f) ‚àß Surjective (Spec.map f) ‚Üî f.hom.FaithfullyFlat := by
  rw [HasRingHomProperty.Spec_iff (P := @Flat)]
  rw [RingHom.FaithfullyFlat.iff_flat_and_comap_surjective, surjective_iff]
  rfl

lemma isIso_SpecMap_iff {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    IsIso (Spec.map f) ‚Üî Function.Bijective f.hom := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ have : IsIso (Spec.map f).appTop := inferInstance
    rw [‚Üê ConcreteCategory.isIso_iff_bijective]
    show isomorphisms _ _
    rwa [(isomorphisms _).arrow_mk_iso_iff (arrowIsoŒìSpecOfIsAffine f)]
  ¬∑ have : IsIso f := by rwa [ConcreteCategory.isIso_iff_bijective]
    infer_instance

instance Flat.universallyInjective_descendsAlong_surjective_inf_flat_inf_quasicompact :
    DescendsAlong @UniversallyInjective (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
  apply DescendsAlong.mk'
  introv hf hfst
  rw [UniversallyInjective.iff_diagonal] at hfst ‚ä¢
  have heq : pullback.fst (pullback.fst (pullback.snd g g ‚â´ g) f) (pullback.diagonal g) =
      (pullbackSymmetry _ _).hom ‚â´
      (pullbackRightPullbackFstIso _ _ _).hom ‚â´
      (pullback.congrHom (by simp) rfl).hom ‚â´
      (pullbackSymmetry _ _).hom ‚â´
      pullback.diagonal (pullback.fst f g) ‚â´
      (diagonalObjPullbackFstIso f g).hom := by
    apply pullback.hom_ext
    apply pullback.hom_ext <;> simp [pullback.condition]
    simp [pullback.condition]
  apply MorphismProperty.of_pullback_fst_of_descendsAlong
      (P := @Surjective) (Q := @Surjective ‚äì @Flat ‚äì @QuasiCompact)
      (f := pullback.fst (pullback.snd g g ‚â´ g) f)
  ¬∑ exact MorphismProperty.pullback_fst _ _ hf
  ¬∑ rw [heq]
    iterate 4 rw [cancel_left_of_respectsIso (P := @Surjective)]
    rwa [cancel_right_of_respectsIso (P := @Surjective)]

instance Flat.isomorphisms_descendsAlong_surjective_inf_flat_inf_quasicompact :
    (isomorphisms Scheme.{u}).DescendsAlong (@Surjective ‚äì @Flat ‚äì @QuasiCompact) := by
  apply IsLocalAtTarget.descendsAlong_inf_quasiCompact
  ¬∑ intro X _
    exact X.exists_hom_isAffine_of_isLocalAtSource _ @Flat le_rfl
  ¬∑ intro R S Y œÜ g h (hfst : IsIso _)
    have : IsAffine Y :=
      have : IsIso (pullback.fst (Spec.map œÜ) g) := ‚Äπ_‚Ä∫
      have : UniversallyInjective g := by
        apply MorphismProperty.of_pullback_fst_of_descendsAlong
          (P := @UniversallyInjective) (Q := @Surjective ‚äì @Flat ‚äì @QuasiCompact) (f := Spec.map œÜ)
        exact ‚ü®h, inferInstance‚ü©
        infer_instance
      have : Surjective g := by
        apply MorphismProperty.of_pullback_fst_of_descendsAlong
          (P := @Surjective) (Q := @Surjective ‚äì @Flat ‚äì @QuasiCompact) (f := Spec.map œÜ)
        exact ‚ü®h, inferInstance‚ü©
        infer_instance
      have hopen' : UniversallyOpen g :=
        MorphismProperty.of_pullback_fst_of_descendsAlong
          (P := @UniversallyOpen) (Q := @Surjective ‚äì @Flat ‚äì @QuasiCompact) (f := Spec.map œÜ)
          ‚ü®h, inferInstance‚ü© inferInstance
      have : IsHomeomorph g.base :=
        ‚ü®g.continuous, g.isOpenMap, g.injective, g.surjective‚ü©
      have : IsAffineHom g :=
        AlgebraicGeometry.isAffineHom_of_isInducing g this.isInducing
          this.isClosedEmbedding.isClosed_range
      isAffine_of_isAffineHom g
    wlog hY : ‚àÉ T, Y = Spec T generalizing Y
    ¬∑ rw [‚Üê (isomorphisms Scheme).cancel_left_of_respectsIso Y.isoSpec.inv]
      have heq : pullback.fst (Spec.map œÜ) (Y.isoSpec.inv ‚â´ g) =
          pullback.map _ _ _ _ (ùüô _) (Y.isoSpec.inv) (ùüô _) (by simp) (by simp) ‚â´
            pullback.fst (Spec.map œÜ) g := (pullback.lift_fst _ _ _).symm
      refine this _ ?_ ?_ ‚ü®_, rfl‚ü©
      ¬∑ show isomorphisms Scheme _
        rwa [heq, (isomorphisms Scheme).cancel_left_of_respectsIso]
      ¬∑ infer_instance
    obtain ‚ü®T, rfl‚ü© := hY
    obtain ‚ü®œà, rfl‚ü© := Spec.map_surjective g
    apply of_pullback_fst_Spec_of_codescendsAlong (P := isomorphisms Scheme.{u})
      (Q' := RingHom.FaithfullyFlat) (Q := fun f ‚Ü¶ Function.Bijective f) (P' := @Surjective ‚äì @Flat)
    ¬∑ exact RingHom.FaithfullyFlat.bijective_codescendsAlong
    ¬∑ intro _ _ f hf
      rwa [‚Üê flat_and_surjective_SpecMap_iff, and_comm]
    ¬∑ simp_rw [‚Üê isIso_SpecMap_iff]
      rfl
    ¬∑ exact h
    ¬∑ exact hfst

lemma _root_.RingHom.Flat.isOpenMap_comap_of_finitePresentation
    {R S : Type*} [CommRing R] [CommRing S] {f : R ‚Üí+* S} (hf : f.Flat)
    (hfin : f.FinitePresentation) :
    IsOpenMap (PrimeSpectrum.comap f) := by
  algebraize [f]
  exact PrimeSpectrum.isOpenMap_comap_of_hasGoingDown_of_finitePresentation

lemma of_generalizingMap {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [LocallyOfFinitePresentation f]
    (hf : GeneralizingMap f.base) : IsOpenMap f.base := by
  show topologically IsOpenMap f
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ rw [IsLocalAtTarget.iff_of_openCover (P := topologically IsOpenMap) Y.affineCover]
    intro i
    dsimp only [Scheme.Cover.pullbackHom]
    refine this _ ?_ ‚ü®_, rfl‚ü©
    exact IsLocalAtTarget.of_isPullback (P := topologically GeneralizingMap)
      (iY := Y.affineCover.map i) (IsPullback.of_hasPullback ..) hf
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ rw [IsLocalAtSource.iff_of_openCover (P := topologically IsOpenMap) X.affineCover]
    intro i
    refine this _ _ ?_ ‚ü®_, rfl‚ü©
    exact IsLocalAtSource.comp (P := topologically GeneralizingMap) hf _
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  algebraize [œÜ.hom]
  convert PrimeSpectrum.isOpenMap_comap_of_hasGoingDown_of_finitePresentation
  ¬∑ rwa [Algebra.HasGoingDown.iff_generalizingMap_primeSpectrumComap]
  ¬∑ apply (HasRingHomProperty.Spec_iff (P := @LocallyOfFinitePresentation)).mp inferInstance

lemma Flat.generalizingMap {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [hf : Flat f] :
    GeneralizingMap f.base := by
  have := HasRingHomProperty.of_isLocalAtSource_of_isLocalAtTarget.{u}
    (topologically GeneralizingMap)
  show topologically GeneralizingMap f
  rw [HasRingHomProperty.iff_appLE (P := topologically GeneralizingMap)]
  intro U V e
  algebraize [(f.appLE U V e).hom]
  apply Algebra.HasGoingDown.iff_generalizingMap_primeSpectrumComap.mp
  convert Algebra.HasGoingDown.of_flat
  exact hf.1 U V e

instance (priority := low) Flat.universallyOpen {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [Flat f]
    [LocallyOfFinitePresentation f] : UniversallyOpen f :=
  ‚ü®universally_mk' _ _ fun _ _ ‚Ü¶ of_generalizingMap _ (Flat.generalizingMap _)‚ü©

instance (priority := low) Flat.isIso {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [Flat f]
    [QuasiCompact f] [Surjective f] [Mono f] : IsIso f := by
  apply MorphismProperty.of_pullback_fst_of_descendsAlong
    (P := isomorphisms Scheme.{u}) (Q := @Surjective ‚äì @Flat ‚äì @QuasiCompact) (f := f) (g := f)
  ¬∑ tauto
  ¬∑ exact inferInstanceAs <| IsIso (pullback.fst f f)

instance (priority := low) Flat.isOpenImmersion {X Y : Scheme.{u}} (f : X ‚ü∂ Y) [Flat f]
    [LocallyOfFinitePresentation f] [Mono f] : IsOpenImmersion f := by
  wlog hf : Surjective f
  ¬∑ let U : Y.Opens := ‚ü®Set.range f.base, f.isOpenMap.isOpen_range‚ü©
    have hU : IsOpenImmersion U.Œπ := U.instIsOpenImmersionŒπ
    let f' : X ‚ü∂ U := AlgebraicGeometry.IsOpenImmersion.lift U.Œπ f (by simp [U])
    have heq : f = f' ‚â´ U.Œπ := by simp [f']
    have hflat : Flat f' := by
      refine of_postcomp (W := @Flat) (W' := @IsOpenImmersion) f' U.Œπ ?_ ?_
      ¬∑ infer_instance
      ¬∑ rwa [‚Üê heq]
    have hfinpres : LocallyOfFinitePresentation f' := by
      refine of_postcomp (W := @LocallyOfFinitePresentation) (W' := @IsOpenImmersion) f' U.Œπ ?_ ?_
      ¬∑ infer_instance
      ¬∑ rwa [‚Üê heq]
    have hmono : Mono f' := by
      convert mono_of_mono f' U.Œπ
      rwa [‚Üê heq]
    rw [heq]
    have := this f' ‚ü®fun x : U ‚Ü¶ by
      obtain ‚ü®a, ha‚ü© := x.2
      use a
      apply Subtype.ext
      rw [‚Üê ha]
      simp only [f']
      conv_rhs => rw [heq]
      simp only [Scheme.comp_coeBase, TopCat.hom_comp, ContinuousMap.comp_apply,
        Scheme.Opens.Œπ_base_apply, SetLike.coe_eq_coe, f']‚ü©
    apply IsOpenImmersion.comp
  have hhomeo : IsHomeomorph f.base :=
    ‚ü®f.continuous, f.isOpenMap, f.injective, f.surjective‚ü©
  have : QuasiCompact f := ‚ü®fun U hU hc ‚Ü¶ (hhomeo.homeomorph).isCompact_preimage.mpr hc‚ü©
  infer_instance

end AlgebraicGeometry
